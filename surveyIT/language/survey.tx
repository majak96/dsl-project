import question_types

Survey:
    //user defined question types
    imports*=Import

    //survey
    survey=SurveyContent
;

Import: 'import' importURI=STRING;

SurveyContent:
    'survey' name=ID '{'
        //survey information
        survey_info=SurveyInformation

        //sections with questions
        sections+=Section
    '}'
;

SurveyInformation:
    'title' ':' title=STRING
    ('description' ':' description=STRING)?
    ('author' ':' author=STRING)?

    'submit_url' ':' submit_url=STRING
    
    ('success_message' ':' success_message=STRING)?
    ('error_message' ':' error_message=STRING)?
;

Section:
    'section' '{'
        ('title' ':' title=STRING)?
        ('description' ':' description=STRING)?
    
        questions+=Question
    '}'
;

Question:
    required?='@Required'
    '@' type=[QuestionType]
    'question' name=ID '{'
        'title' ':' title=STRING
        parameters*=ParameterValue
    '}'
;

ParameterValue:
    parameter=[Parameter] ':' value=Value
    
;

Value:
    PrimitiveValue | ArrayValue
;

PrimitiveValue:
    value=STRING | value=STRICTFLOAT | value=INT | value=BOOL
;

ArrayValue:
    '[' value+=STRING[','] ']' | '[' value+=STRICTFLOAT[','] ']' | '[' value+=INT[','] ']' | '[' value+=BOOL[','] ']' 
;

Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;